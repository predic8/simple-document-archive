class Deployment {
	def descriptor
	def clusterUrl
	
    def descriptor(def descriptor) {
		this.descriptor = descriptor
	}
	def clusterUrl(def clusterUrl) {
		this.clusterUrl = clusterUrl
	}
}

def deployment = project.extensions.create 'deployment', Deployment

afterEvaluate {
	
	project.defaultTasks 'apply'
	
	def deploymentName = deployment.descriptor.metadata.name
	
	task createDeploymentDescriptor() {
		doLast {
			def o = deployment.descriptor
			def outputDir = file("${project.buildDir}/generated/kubernetes")
			if (!outputDir.exists() && !outputDir.mkdirs())
				throw new Exception("Could not create ${outputDir}");
			new File(outputDir, "deployment.json").text = groovy.json.JsonOutput.toJson(o)
		}
	}

    task updateDeployment(dependsOn: ["createDeploymentDescriptor", 'testDockerImage']) {
        doLast {
            def proc = "kubectl -s ${deployment.clusterUrl} apply -f generated/kubernetes/deployment.json".execute(null, project.buildDir)
            def sout = new StringBuilder(), serr = new StringBuilder()
            proc.consumeProcessOutput(sout, serr)
            proc.waitForOrKill(10000)
            println "out> $sout err> $serr"
            if (proc.exitValue() != 0)
                throw new RuntimeException("Exit code is not zero.");
        }
    }

	task apply (dependsOn: ['updateDeployment']){
	}

}
